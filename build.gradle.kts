plugins {
    `java-library`
    id("com.gradleup.shadow") version "9.0.0-beta11"
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.21" apply false
    id("re.alwyn974.groupez.repository") version "1.0.0"
}

group = "fr.maxlego08.essentials"
version = "1.2.1.0"

extra.set("targetFolder", file("target/"))
extra.set("targetFolderDiscord", file("target-discord/"))
extra.set("apiFolder", file("target-api/"))
extra.set("classifier", System.getProperty("archive.classifier"))
extra.set("sha", System.getProperty("github.sha"))

allprojects {
    apply(plugin = "java-library")
    apply(plugin = "com.gradleup.shadow")
    apply(plugin = "re.alwyn974.groupez.repository")

    group = "fr.maxlego08.essentials"
    version = rootProject.version

    repositories {
        mavenLocal()
        mavenCentral()

        maven(url = "https://jitpack.io")
        maven(url = "https://repo.papermc.io/repository/maven-public/")
        maven(url = "https://repo.extendedclip.com/content/repositories/placeholderapi/")
        maven(url = "https://repo.tcoded.com/releases")
        maven {
            name = "faststatsReleases"
            url = uri("https://repo.faststats.dev/releases")
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == "net.kyori" && requested.name == "adventure-text-serializer-ansi" && (requested.version.isNullOrBlank() || requested.version == ".")) {
                useVersion("4.20.0")
            }
        }
    }

    java {
        withSourcesJar()

        // Allow the Java 21 main module to depend on the Java 25 NMS module for Minecraft 26.x.
        disableAutoTargetJvm()

        if (!project.path.startsWith(":NMS:")) {
            withJavadocJar()
        }
    }

    tasks.shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveAppendix.set(if (project.path == ":") "" else project.name)
        archiveClassifier.set("")
    }

    tasks.compileJava {
        options.encoding = "UTF-8"
        options.release = 21
    }

    tasks.javadoc {
        options.encoding = "UTF-8"
        if (JavaVersion.current().isJava9Compatible)
            (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
    }

    dependencies {
//        compileOnly("fr.maxlego08.menu:zmenu-api:1.1.0.0")
        compileOnly(files("libs/zMenu-1.1.1.6.jar"))

        compileOnly("fr.maxlego08.sarah:sarah:1.23")
        compileOnly("com.tcoded:FoliaLib:0.5.1")
        compileOnly("fr.mrmicky:fastboard:2.2.1")

        // Test dependencies
        testImplementation(platform("org.junit:junit-bom:5.11.4"))
        testImplementation("org.junit.jupiter:junit-jupiter")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testImplementation("org.mockito:mockito-core:5.14.2")
        testImplementation("org.mockito:mockito-junit-jupiter:5.14.2")
        testImplementation("net.kyori:adventure-api:4.20.0")
        testImplementation("net.kyori:adventure-text-serializer-legacy:4.20.0")
        testImplementation(files("libs/zMenu-1.1.1.6.jar"))
    }

    tasks.test {
        useJUnitPlatform()
    }
}

dependencies {
    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("io.papermc.paper:paper-api:26.2.build.119-stable")
    testImplementation("io.papermc.paper:paper-api:26.2.build.119-stable")
    testImplementation("com.tcoded:FoliaLib:0.5.1")
    compileOnly("org.mongodb:mongodb-driver-sync:5.2.1")
    compileOnly("net.dmulloy2:ProtocolLib:5.4.0")

    implementation("dev.faststats.metrics:bukkit:0.29.4") {
        exclude(group = "com.google.code.gson", module = "gson")
    }

    api(project(":API"))

    // Since Minecraft 1.20.5 Paper ships a Mojang-mapped runtime, and since 26.1 Spigot
    // reobfuscation is gone entirely. Every NMS module is therefore Mojang-mapped
    // (MOJANG_PRODUCTION) and consumed as a plain project dependency (no "reobf" variant).
    api(project(":NMS:V1_20_6"))
    api(project(":NMS:V1_21"))
    api(project(":NMS:V1_21_1"))
    api(project(":NMS:V1_21_3"))
    api(project(":NMS:V1_21_4"))
    api(project(":NMS:V1_21_5"))
    api(project(":NMS:V1_21_6"))
    api(project(":NMS:V1_21_7"))
    api(project(":NMS:V1_21_8"))
    api(project(":NMS:V1_21_9"))
    api(project(":NMS:V1_21_10"))
    api(project(":NMS:V1_21_11"))
    api(project(":NMS:V26_2"))

    rootProject.subprojects.filter { it.path.startsWith(":Hooks:") }.forEach { subproject ->
        api(project(subproject.path))
    }
}

tasks {
    shadowJar {
        relocate("com.tcoded.folialib", "fr.maxlego08.essentials.libs.folialib")
        relocate("fr.maxlego08.sarah", "fr.maxlego08.essentials.libs.sarah")
        relocate("fr.mrmicky.fastboard", "fr.maxlego08.essentials.libs.fastboard")
        relocate("dev.faststats", "fr.maxlego08.essentials.libs.faststats")

        manifest {
            attributes["paperweight-mappings-namespace"] = "mojang"
        }

        rootProject.extra.properties["sha"]?.let { sha ->
            archiveClassifier.set("${rootProject.extra.properties["classifier"]}-${sha}")
        } ?: run {
            archiveClassifier.set(rootProject.extra.properties["classifier"] as String?)
        }
        destinationDirectory.set(rootProject.extra["targetFolder"] as File)
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        from("resources")
        // Force re-expansion whenever the release version changes,
        // otherwise up-to-date caches can ship a stale plugin.yml
        inputs.property("pluginVersion", project.version.toString())
        filesMatching("plugin.yml") {
            expand("version" to project.version.toString())
        }
    }
}
